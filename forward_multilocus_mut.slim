// CLASSICAL FORWARD SIMULATION OF MULTILOCUS FLUCTUATING SELECTION

initialize() {
	
	defineConstant("GenomeSize", 1000000);  // genome size
	
	defineConstant("s", 1); // selection coefficient
	
	defineConstant("n", 1e4); //  scaled population size
	
	defineConstant("d", 0.6);  //  dominance coefficient
	
	defineConstant("g_s", 10);  // generations per summer
	
	defineConstant("g_w", 10);  // generations per winter 
	
	defineConstant("g", (g_s + g_w));  // generations per seasonal cycle     
	
	defineConstant("mut", 1e-7); // neutral mutation rate
	
	defineConstant("rr", 1e-6); // scaled recombination rate
	
	if (!exists("sim_run"))
	{defineConstant("sim_run", 1);} // simulation identifier
	
	if (!exists("fit"))
	{defineConstant("fit",1);} //if fit = 0 fitness function switched off
	
	initializeMutationRate(mut);
	
	initializeRecombinationRate(rr);
	
	initializeMutationType("m1", 0.5, "f", 0.0);  // neutral mutations
	initializeMutationType("m2", 0.0, "f", 0.0);  // seasonal mutations
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, GenomeSize-1);
	m2.color = "pink";
	m2.convertToSubstitution = F;
}


1 late() {
	// read in popultion from classical burn-in
	sim.readFromPopulationFile(paste("~/2023_popgen_benchmarking/burnin_mut_ "+ asString(sim_run) + " .txt"));}

100001 late(){
	// position of mutations evenly distributed across sequence
	win = (GenomeSize)/10;
	win_mid = win/2;
	
	// drawn single selected mutation at each position in a single individual until 10 selected mutations have been included
	for (i in 1:10){
		pos = (i*win)-win_mid; // evenly distribute mutations	
		target = sample(p1.genomes, asInteger(1));
		target.addNewDrawnMutation(m2, asInteger(pos));
	}
	
	// output sample of 100 individuals
	p1.outputSample(100, replace=F, filePath=paste("~/2023_popgen_benchmarking/forward_multi_mut"+ sim_run +".txt"));
}

// multilocus fluctuating selection fitness model from Wittmann et al. (2017) 
s1 fitnessEffect() { //summer
	if (fit == 0|sim.cycle<=100000){
		return 1.0;
	} else{
		genomes=individual.genomes.mutationFrequenciesInGenomes(mutations=sim.mutationsOfType(m2));
		ns = sum(genomes==1); //homozygosity summer
		nhet = sum(genomes==0.5);  // heterozygosity
		nw = sum(genomes==0);//homozygosity winter
		if (modValueSeason >= 0 & modValueSeason < g_s) { //summer
			zs = ns + (d * nhet);
			return (1 + zs)^4; }
		else { // winter
			zw = nw + (d * nhet);
			return (1 + zw)^4;}
	}
}


100001:140000 early(){
	defineGlobal("gen", sim.cycle);
	defineGlobal("modValueSeason",integerMod(gen, g));
}

100001:140000 late(){
	// track fixed mutations
	defineGlobal("fixed", sum(sim.mutationFrequencies(p1)==1));
	
	// binary 2-season environment
	if (modValueSeason >= 0 & modValueSeason < g_s) { //summer
		
		if (exists("slimgui")) {cat("Summer\n");}
	}
	if (modValueSeason >= g_s) { //winter
		
		if (exists("slimgui")) {cat("Winter\n");}
	}
	
	// restart simulation if selected mutation is lost
	if (sim.countOfMutationsOfType(m2) == 0)
	{outcome = paste(sim_run + ": LOST at gen "+ gen+" â€“ RESTARTING\n");
		catn(outcome);
		sim.readFromPopulationFile(paste("~/2023_popgen_benchmarking/burnin_mut_ "+ asString(sim_run) + " .txt"));
	}
	
	// smaple 100 individuals every 10,000 generations
	if (integerMod(gen, 10000)==0){
		p1.outputSample(100, replace=F, filePath=paste("~/2023_popgen_benchmarking/forward_multi_mut"+ sim_run +".txt"), append=T);
	}

}

140000 late() {
	// output full population in final generation
	sim.outputFull(paste("~/2023_popgen_benchmarking/forward_multi_mut"+sim_run+".txt"), append=T);
	
	//record memeory usage
	mem = usage(peak=T);
	line4 = paste("Memory usage: "+ mem);
	catn(line4);
	
	// output memeory usage and other information to txt file
	writeFile("~/2023_popgen_benchmarking/forward_benchmarking_mem.txt", paste("mut", "multilocus", mem, sep="\t"), append=T);
	sim.simulationFinished();
}
